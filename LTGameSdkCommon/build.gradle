apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven' //必加
group='com.github.muyishuangfeng' //必加,com.github.<用户名>(也就是你GtiHub上的地址)

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    //日志
    implementation 'com.orhanobut:logger:2.2.0'
    //RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    //RxJava2
    implementation 'io.reactivex.rxjava2:rxjava:2.2.1'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    //Retrofit2的RxJava适配
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    //Retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    //RxJava2Gson适配
   //implementation 'com.squareup.retrofit2:converter-gson:2.0.2'
    //网络日志拦截
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    //eventBus
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.google.code.gson:gson:2.8.5'
}

// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}
// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
    archives javadocJar
}
